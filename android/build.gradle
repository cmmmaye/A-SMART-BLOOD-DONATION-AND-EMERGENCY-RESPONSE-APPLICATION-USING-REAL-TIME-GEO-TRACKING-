buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    afterEvaluate { project ->
        if (project.plugins.hasPlugin('com.android.library')) {
            def manifestFile = project.file("src/main/AndroidManifest.xml")
            if (manifestFile.exists()) {
                def manifestText = manifestFile.getText()
                def matcher = (manifestText =~ /package="([^"]+)"/)
                if (matcher.find()) {
                    project.android.namespace = matcher.group(1)
                }
            }
        }
        // Ensure all Android modules compile with Java 17 to avoid source/target 8 warnings
        if (project.plugins.hasPlugin('com.android.application') || project.plugins.hasPlugin('com.android.library')) {
            project.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
        }
        // Ensure Kotlin modules target JVM 17
        if (project.plugins.hasPlugin('org.jetbrains.kotlin.android')) {
            project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                kotlinOptions {
                    jvmTarget = '17'
                }
            }
        }
    }
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.layout.buildDirectory
}
